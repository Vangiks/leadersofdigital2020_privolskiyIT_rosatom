
Процедура ОбработкаПроведения(Отказ, Режим)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Дата КАК Период,
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Ссылка КАК Регистратор,
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Организация КАК Организация,
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Банк КАК Банк,
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Валюта КАК Валюта,
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Сумма КАК Сумма,
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Комментарий КАК Комментарий
		|ИЗ
		|	Документ.ЭД_ЭлектронныйДокумент.ОстаткиПоСчетам КАК ЭД_ЭлектронныйДокументОстаткиПоСчетам
		|ГДЕ
		|	ЭД_ЭлектронныйДокументОстаткиПоСчетам.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Дата КАК Период,
		|	ЭД_ЭлектронныйДокументФинансовыеСделки.Ссылка КАК Регистратор,
		|	ЭД_ЭлектронныйДокументФинансовыеСделки.Организация КАК Организация,
		|	ЭД_ЭлектронныйДокументФинансовыеСделки.Сделка КАК Сделка,
		|	ЭД_ЭлектронныйДокументФинансовыеСделки.Сделка.Валюта КАК Валюта,
		|	ЭД_ЭлектронныйДокументФинансовыеСделки.Сумма КАК Сумма
		|ИЗ
		|	Документ.ЭД_ЭлектронныйДокумент.ФинансовыеСделки КАК ЭД_ЭлектронныйДокументФинансовыеСделки
		|ГДЕ
		|	ЭД_ЭлектронныйДокументФинансовыеСделки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",      Ссылка);
	Запрос.УстановитьПараметр("Дата",        Дата);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДвиженияОстаткиПоСчетам = Движения.ОстаткиПоСчетам;
	ДвиженияОстаткиПоСчетам.Записывать = Истина;
	ДвиженияОстаткиПоСчетам.Загрузить(РезультатЗапроса[0].Выгрузить());
	
	ДвиженияФинансовыеСделки = Движения.ФинансовыеСделки;
	ДвиженияФинансовыеСделки.Записывать = Истина;
	ДвиженияФинансовыеСделки.Загрузить(РезультатЗапроса[1].Выгрузить());
	
КонецПроцедуры
