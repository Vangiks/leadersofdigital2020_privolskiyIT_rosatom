
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = СтрШаблон("%1 (%2)", Заголовок, Пользователи.ТекущийПользователь());
	
	ОбработкаПерехода();
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникиНажатие(Элемент)
	ОбработкаПерехода(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ПодготовкаОтчетностиНажатие(Элемент)
	ОткрытьФорму("Документ.ЭД_ЭлектронныйДокумент.ФормаСписка", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Справочник_Валюты_ФормаСпискаНажатие(Элемент)
	ОбработкаОткрытияФормы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Справочник_Организации_ФормаСпискаНажатие(Элемент)
	ОбработкаОткрытияФормы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Справочник_КлассификаторБанков_ФормаСпискаНажатие(Элемент)
	ОбработкаОткрытияФормы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура Справочник_ФинансовыеСделки_ФормаСпискаНажатие(Элемент)
	ОбработкаОткрытияФормы(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПользователейИПравНажатие(Элемент)
	
	ОткрытьФорму(
		"Обработка.ПанельАдминистрированияБСП.Форма.НастройкиПользователейИПрав",
		,
		ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаЗагрузкиСправочниковНажатие(Элемент)
	
	ОткрытьФорму(
		"Обработка.ПанельАдминистрированияБСП.Форма.ИнтернетПоддержкаИСервисы",
		,
		ЭтотОбъект);

	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаНажатие(Элемент)
	
	ОткрытьФорму("Отчет.ДвижениеДС.Форма", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АдминистраторНажатие(Элемент)
	ОбработкаПерехода(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура НачальнаяСтраницаНажатие(Элемент)
	ОбработкаПерехода();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПерехода(ИмяЭлемента = Неопределено)
	
	Если ИмяЭлемента = Неопределено Тогда
		ИмяГруппыЭлементов = НачальнаяПодсистема();
	Иначе
		ИмяГруппыЭлементов = СтрШаблон("Группа%1", ИмяЭлемента);
	КонецЕсли;
	
	Для Каждого ЭлементФормы Из Элементы Цикл
		
		Если ЭлементФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа Тогда
			
			Если Не СтрНачинаетсяС(ЭлементФормы.Имя, "Группа") Тогда
				Продолжить;	
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы, ЭлементФормы.Имя, "Видимость", ЭлементФормы.Имя = ИмяГруппыЭлементов);
				
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "НачальнаяСтраница", "Видимость", ИмяГруппыЭлементов <> НачальнаяПодсистема());
		
	УстановитьRLS();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОткрытияФормы(Элемент)
	
	Массив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Элемент.Имя, "_");
	Если Массив.Количество() < 3 Тогда
		Возврат;	
	КонецЕсли;
	          
	ОткрытьФорму(СтрШаблон("%1.%2.%3", Массив[0], Массив[1], Массив[2]), , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НачальнаяПодсистема()
	
	Возврат "ГруппаПодсистемы";
	
КонецФункции

&НаСервере
Процедура УстановитьRLS()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Справочники", "Видимость", ПравоДоступа("Просмотр", Метаданные.Подсистемы.НСИ));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ПодготовкаОтчетности", "Видимость", ПравоДоступа("Просмотр", Метаданные.Подсистемы.ОтчётностьБДДС));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Аналитика", "Видимость", ПравоДоступа("Использование", Метаданные.Отчеты.ДвижениеДС));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "Администратор", "Видимость", ПравоДоступа("Просмотр", Метаданные.Подсистемы.Администрирование));
		
КонецПроцедуры
