&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого стр Из Параметры.сзПоказатели Цикл
		
		НС = тзПоказатели.Добавить();
		НС.Показатель = стр.Значение;
		
	КонецЦикла;	
	
	МодельЭД = Параметры.МодельЭД;
	
	Если ЗначениеЗаполнено(МодельЭД) Тогда

		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "МодельЭД");
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НоваяСвязь);
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		
		Элементы.СтрокаЭД.СвязиПараметровВыбора = НовыеСвязи;	
		Элементы.КолонкаЭД.СвязиПараметровВыбора = НовыеСвязи;	
	КонецЕсли;	
	
КонецПроцедуры       


&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНаСервере();
	
	Для Каждого стр Из тзПоказатели Цикл
		ОповеститьОбИзменении(стр.Показатель);
	КонецЦикла;	     	
		
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	мРеквизиты = Новый Массив;
	мРеквизиты.Добавить("СтрокаЭД");
	мРеквизиты.Добавить("КолонкаЭД");
	мРеквизиты.Добавить("Длина");
	мРеквизиты.Добавить("Точность");
	мРеквизиты.Добавить("НеМасштабируется");
	
	ЕстьИзменения = Ложь;
	Для Каждого ИмяРеквизита Из мРеквизиты Цикл
		
		Если ЭтаФорма[ИмяРеквизита + "Выбор"] Тогда
			ЕстьИзменения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;	     			
	
	Если ЕстьИзменения Тогда
		Для Каждого стрПоказатель Из тзПоказатели Цикл
			ПокОбъект = стрПоказатель.Показатель.ПолучитьОбъект();
			
			Для Каждого ИмяРеквизита Из мРеквизиты Цикл
				
				Если ЭтаФорма[ИмяРеквизита + "Выбор"] Тогда
					
					ПокОбъект[ИмяРеквизита] = ЭтаФорма[ИмяРеквизита];
					
				КонецЕсли;	
			КонецЦикла;	
			
			ПокОбъект.Записать();
			стрПоказатель.Показатель = ПокОбъект.Ссылка;
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлагВыбора(ИмяРеквизита)
	
	ЭтаФорма[ИмяРеквизита + "Выбор"] = Истина;

КонецПроцедуры	

&НаКлиенте
Процедура СтрокаЭДПриИзменении(Элемент)
	УстановитьФлагВыбора(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	УстановитьФлагВыбора(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ТочностьПриИзменении(Элемент)
	УстановитьФлагВыбора(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура НеМасштабироватьПриИзменении(Элемент)
	УстановитьФлагВыбора(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура КолонкаЭДПриИзменении(Элемент)
	УстановитьФлагВыбора(Элемент.Имя);
КонецПроцедуры
