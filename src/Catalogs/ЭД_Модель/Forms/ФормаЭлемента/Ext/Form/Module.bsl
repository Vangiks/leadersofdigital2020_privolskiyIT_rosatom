&НаСервере
Процедура СоздатьПоказателиНаСервере(мПоказатели)

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Сообщить("Объект должен быть записан.");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭД_Показатели.Ссылка КАК Ссылка,
	|	ЭД_Показатели.СтрокаЭД КАК СтрокаЭД,
	|	ЭД_Показатели.КолонкаЭД КАК КолонкаЭД
	|ПОМЕСТИТЬ втПоказатели
	|ИЗ
	|	Справочник.ЭД_Показатели КАК ЭД_Показатели
	|ГДЕ
	|	ЭД_Показатели.Владелец = &Модель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭД_Строки.Ссылка КАК Строка,
	|	ЭД_Колонки.Ссылка КАК Колонка
	|ПОМЕСТИТЬ втВсеСтрокиКолонки
	|ИЗ
	|	Справочник.ЭД_Строки КАК ЭД_Строки,
	|	Справочник.ЭД_Колонки КАК ЭД_Колонки
	|ГДЕ
	|	ЭД_Строки.Владелец = &Модель
	|	И ЭД_Колонки.Владелец = &Модель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВсеСтрокиКолонки.Строка КАК Строка,
	|	втВсеСтрокиКолонки.Колонка КАК Колонка,
	|	втВсеСтрокиКолонки.Строка.Наименование КАК СтрокаНаименование,
	|	втВсеСтрокиКолонки.Колонка.Наименование КАК КолонкаНаименование
	|ИЗ
	|	втВсеСтрокиКолонки КАК втВсеСтрокиКолонки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПоказатели КАК втПоказатели
	|		ПО втВсеСтрокиКолонки.Строка = втПоказатели.СтрокаЭД
	|			И втВсеСтрокиКолонки.Колонка = втПоказатели.КолонкаЭД
	|ГДЕ
	|	втПоказатели.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Модель", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл    		
		НовыйПоказатель 	= Справочники.ЭД_Показатели.СоздатьЭлемент();
		НовыйПоказатель.Наименование 	= Выборка.СтрокаНаименование + " " + Выборка.КолонкаНаименование;
		НовыйПоказатель.Владелец 		= Объект.Ссылка;
		НовыйПоказатель.СтрокаЭД 		= Выборка.Строка;
		НовыйПоказатель.КолонкаЭД 		= Выборка.Колонка;
		НовыйПоказатель.Длина			= Выборка.Колонка.Длина;
		НовыйПоказатель.Точность		= Выборка.Колонка.Точность;
		НовыйПоказатель.НеМасштабируется= Выборка.Колонка.НеМасштабируется;
		НовыйПоказатель.Записать();
		мПоказатели.Добавить(НовыйПоказатель.Ссылка);
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоказатели(Команда)
	
	мПоказатели = Новый Массив;	
	СоздатьПоказателиНаСервере(мПоказатели);
	Для Каждого стр Из мПоказатели Цикл
		ОповеститьОбИзменении(стр);
	КонецЦикла;	     	

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСвойства(Команда)
	
	СтруктураПараметров = Новый Структура;
	
	ПолеПоиск = Новый ПолеКомпоновкиДанных("Владелец");
	
	СтруктураПараметров.Вставить("МодельЭД", Объект.Ссылка);
	
	сзПоказатели = Новый СписокЗначений;
	сзПоказатели.ЗагрузитьЗначения(Элементы.Показатели.ВыделенныеСтроки);
	
	СтруктураПараметров.Вставить("сзПоказатели", сзПоказатели);
	
	Форма = ОткрытьФорму("ОбщаяФорма.ЭД_ФормаРедактированияРеквизитов", СтруктураПараметров);	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметры(ТекСсылка)
	
	Показатели.Параметры.УстановитьЗначениеПараметра("Владелец", ТекСсылка);
	Строки.Параметры.УстановитьЗначениеПараметра("Владелец", ТекСсылка);
	Колонки.Параметры.УстановитьЗначениеПараметра("Владелец", ТекСсылка);
	МакетыИмпорта.Параметры.УстановитьЗначениеПараметра("Владелец", ТекСсылка);
	
КонецПроцедуры	


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		УстановитьПараметры(Объект.Ссылка);
КонецПроцедуры

